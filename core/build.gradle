import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
sourceCompatibility = 1.5
version = '1.4.1'
archivesBaseName = 'rundeck-core'
defaultTasks 'clean','assemble'
repositories {
    mavenCentral()
    add(new org.apache.ivy.plugins.resolver.FileSystemResolver()) {
        name = 'localdeps'
        //addIvyPattern "$projectDir/../dependencies/[organisation]/[module]-ivy-[revision].xml"
        addArtifactPattern "$projectDir/../dependencies/[module]/[ext]s/[module]-[revision](-[classifier]).[ext]"
        descriptor = 'optional'
        checkmodified = true
    }
}
dependencies {
    compile( 
        [group: 'org.apache.ant', name: 'ant', version: '1.7.1',ext:'jar'],
        [group: 'org.apache.ant', name: 'ant-launcher', version: '1.7.1',ext:'jar'],
        [group: 'log4j', name: 'log4j', version: '1.2.16',ext:'jar'],
        [group: 'commons-codec', name: 'commons-codec', version: '1.5',ext:'jar'],
        [group: 'commons-httpclient', name: 'commons-httpclient', version: '3.0.1',ext:'jar'],
        [group: 'commons-beanutils', name: 'commons-beanutils', version: '1.8.0',ext:'jar'],
        [group: 'commons-collections', name: 'commons-collections', version: '3.2.1',ext:'jar'],
        [group: 'commons-logging', name: 'commons-logging', version: '1.1',ext:'jar'],
        [group: 'commons-lang', name: 'commons-lang', version: '2.4',ext:'jar'],
        [group: 'xerces', name: 'xercesImpl', version: '2.6.0',ext:'jar'],
        [group: 'dom4j', name: 'dom4j', version: '1.6.1',ext:'jar'],
        [group: 'jaxen', name: 'jaxen', version: '1.1',ext:'jar'],
        [group: 'commons-cli', name: 'commons-cli', version: '1.0',ext:'jar'],
        [group: 'org.apache.ant', name: 'ant-jsch', version: '1.7.1',ext:'jar'],
        [group: 'com.jcraft', name: 'jsch', version: '0.1.42',ext:'jar'],
        [group: 'org.yaml',name:'snakeyaml',version:'1.9']
    )
    testCompile(
        [group: 'junit', name: 'junit', version: '3.8.1',ext:'jar']
    )
}

jar {
    manifest {
        attributes 'Rundeck-Version': version,'Rundeck-Tools-Dependencies':configurations.runtime.collect { "$it.name" }.join(" ") 
    }
}

test{
    systemProperties 'rdeck.base': "$projectDir/build/rdeck_base"
    systemProperties 'rdeck.home': "$projectDir/build"
}

task expandTemplate << {
    ant.delete(file:"$projectDir/src/main/resources/META-INF/com/dtolabs/rundeck/core/application.properties")
    copy{
        expand('version':version,'version_build':buildNum,'version_ident':version+'-'+buildNum)
        from "$projectDir/src/main/meta/com/dtolabs/rundeck/core/application.properties"
        into "$projectDir/src/main/resources/META-INF/com/dtolabs/rundeck/core/"
    }   
}

compileJava.dependsOn expandTemplate

assemble {
    doLast{
        //copy libs to a tools lib dir
        ant.mkdir(dir:"$projectDir/build/tools/lib")
        configurations.runtime.files.each { file ->
            copy{
                from file
                into "$projectDir/build/tools/lib"
            }
        }
    }
}

uploadArchives {
    repositories {
       flatDir(dirs: file('repos'))
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '0.9-rc-3'
    jarPath = 'wrapper'
}

apply plugin: 'maven'
task createPom << {
    pom {
        project {
            artifactId archivesBaseName
            groupId "com.dtolabs.rundeck"
            inceptionYear '2011'
            packaging 'jar'
            version version
            name "RunDeck Core"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            properties{
                'version'(version)
                'version_build'(buildNum)
                'version_ident'(version+'-'+buildNum)
            }
            build {
                resources{
                    resource{
                        directory 'src/main/meta'
                        targetPath 'META-INF'
                        filtering=true
                    }
                    resource{
                        directory 'src/main/resources'
                    }
                }
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '2.3.2'
                        configuration{
                            'source'('1.5')
                            'target'('1.5')
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-surefire-plugin'
                        version '2.10'
                        configuration{
                            systemPropertyVariables{
                                'rdeck.base'('${project.build.directory}/rdeck_base')
                            }
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            archive{
                              manifestEntries{
                                'Rundeck-Version'(version)
                                'Rundeck-Tools-Dependencies'(configurations.runtime.collect { "$it.name" }.join(" "))
                              }
                            }
                        }
                    }
                }
            }
        }
    }.writeTo("pom.xml")
}
